plugins {
    id 'java-library'
    id "io.freefair.lombok" version "6.3.0"
    id 'org.jreleaser' version '1.19.0'
}

group 'io.github.zhongzichang'
version '1.0.1'

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
    maven { url "https://maven.aliyun.com/repository/spring" }
    maven { url "https://maven.aliyun.com/repository/spring-plugin" }
    maven { url "https://maven.aliyun.com/repository/google" }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    mavenCentral()

}

dependencies {

    implementation 'com.auth0:java-jwt:4.3.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    
    publications {
        
        maven(MavenPublication) {

            from components.java

            pom {

                name = 'spring-iap'
                description = 'SDK for validating in-app transaction receipts and handling notifications from app store server.'
                url = 'https://github.com/zhongzichang/spring-iap'

                licenses {
                    license {
                        name = 'GNU Lesser General Public License, version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
                    }
                }

                developers {
                    developer {
                        id = 'zczhong'
                        name = 'Zhong ZiChang'
                        email = 'zhongzichang@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:https@github.com:zhongzichang/spring-iap.git'
                    developerConnection = 'scm:git:git@github.com:zhongzichang/spring-iap.git'
                    url = 'https://github.com/zhongzichang/spring-iap'
                }
            }
        }
    }

    repositories {

        maven {
            url = layout.buildDirectory.dir("staging-deploy")
        }

    }

}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
